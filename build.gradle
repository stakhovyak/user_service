plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.google.protobuf' version '0.9.4'
}

group = 'com.weuoimi'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.21.12' // compiler
	}

	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.53.0' // java code generation plugin
		}

		reactor {
			artifact = "com.salesforce.servicelibs:reactor-grpc:1.0.1" // generates reactive webflux source code
		}
	}

	generateProtoTasks {
		ofSourceSet("main")*.plugins {
			grpc {}
			reactor {}
		}
	}
}

// Include the generated code in the main source set
sourceSets {
    main {
        java {
            srcDir 'build/generated/source/proto/main/java'
        }
    }
}

dependencies {
	// Spring web (reactive)
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// gRPC deps
	implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.2.3'
	implementation 'net.devh:grpc-server-spring-boot-starter:3.1.0.RELEASE'
	implementation 'io.grpc:grpc-protobuf:1.66.0'
	implementation 'io.grpc:grpc-stub:1.66.0'
	implementation 'com.google.protobuf:protobuf-java-util:4.28.0-RC2'
	
	// postgresql driver jdbc
	implementation 'org.postgresql:postgresql:42.7.3'
	implementation 'org.postgresql:r2dbc-postgresql:1.0.5.RELEASE'

	// jdbc & r2dbc
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc:3.3.2'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc:3.3.2'

	// Lombok and annotations-api
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	
	// Testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}
